plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'io.freefair.lombok' version '8.11'
    id 'com.gradle.plugin-publish' version '1.3.0'
}

group 'de.lukaskoerfer.gradle'
version '0.3.0'
description 'Adds support for extended debugging options'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation localGroovy()
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0', {
        exclude group: 'org.codehaus.groovy'
    }
    // java.lang.IllegalAccessError: class lombok.javac.apt.LombokProcessor (in unnamed module @0x78ba8a69) cannot access class com.sun.tools.javac.processing.JavacProcessingEnvironment (in module jdk.compiler)
    // https://stackoverflow.com/questions/66801256/java-lang-illegalaccesserror-class-lombok-javac-apt-lombokprocessor-cannot-acce
    lombok 'org.projectlombok:lombok:1.18.36'
}

java {
    withSourcesJar()
    withJavadocJar()
    lombok.version = '1.18.12'
}

testing {
    suites {
        test {
            useJUnit()
        }
    }
}

javadoc {
	options.linksOffline 'https://docs.oracle.com/javase/8/docs/api/',
        'https://docs.gradle.org/current/javadoc/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.addBooleanOption 'html5', true
}

gradlePlugin {
    website = 'https://github.com/lukoerfer/gradle-debugging'
    vcsUrl = 'https://github.com/lukoerfer/gradle-debugging'
    plugins {
        plugin {
            id = 'de.lukaskoerfer.gradle.debugging'
            implementationClass = 'de.lukaskoerfer.gradle.debugging.DebuggingPlugin'
            displayName = 'Debugging Plugin'
            description = project.description
            tags = ['debugging', 'debug', 'remote', 'plugin']
        }
    }
}
