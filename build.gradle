plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.3.0'
}

group = 'de.lukaskoerfer.gradle'
version = '0.3.0'
description = 'Adds support for extended debugging options'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation localGroovy()
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0', {
        exclude group: 'org.codehaus.groovy'
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

testing {
    suites {
        test {
            useJUnit()
        }
    }
}

javadoc {
    options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/',
        'https://docs.gradle.org/current/javadoc/'
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.addBooleanOption 'html5', true
}

gradlePlugin {
    website = 'https://github.com/lukoerfer/gradle-debugging'
    vcsUrl = 'https://github.com/lukoerfer/gradle-debugging'
    plugins {
        plugin {
            id = 'de.lukaskoerfer.gradle.debugging'
            implementationClass = 'de.lukaskoerfer.gradle.debugging.DebuggingPlugin'
            displayName = 'Debugging Plugin'
            description = project.description
            tags = ['debugging', 'debug', 'remote', 'plugin']
        }
    }
}
